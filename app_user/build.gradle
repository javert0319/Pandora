if (rootProject.ext.is_build_module) {
    apply plugin: 'com.android.application'
} else {
    apply plugin: 'com.android.library'
}
apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion rootProject.ext.android.compileSdkVersion
    defaultConfig {
        minSdkVersion rootProject.ext.android.minSdkVersion
        targetSdkVersion rootProject.ext.android.targetSdkVersion
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        javaCompileOptions {
            kapt {
                arguments {
                    arg("AROUTER_MODULE_NAME", project.getName())
                }
            }
        }
        multiDexEnabled true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        main {
            if (rootProject.ext.is_build_module) {
                manifest.srcFile 'src/main/debug/AndroidManifest.xml'
                java.srcDirs += "src/main/debug/java/"
                res.srcDirs += "src/main/debug/res/"
            } else {
                manifest.srcFile 'src/main/release/AndroidManifest.xml'
                java.srcDirs += "src/main/release/java/"
                resources {
                    exclude 'src/debug/*'
                }
            }
            java.srcDirs += 'src/main/kotlin'
        }
    }


    resourcePrefix "user_"

    dataBinding {
        enabled true
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    api project(path: ':lib_fram')
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    kapt rootProject.ext.dependencies.arouter_compiler
}
